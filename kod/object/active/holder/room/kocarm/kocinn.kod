// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
KocatanInn is KocatanRoom

constants:

   include blakston.khd

resources:

   include kocinn.lkod
  
   room_kocinn = kocinn.roo
   room_name_kocinn = "The Aerie Guest House"

   kocinn_waz_locked = "Room waz is locked."
   kocinn_wazwaz_locked = "Room wazwaz is locked."
   kocinn_zik_locked = "Room zik is locked."
   kocinn_zikzik_locked = "Room zikzik is locked."
   kocinn_katalin_locked = "Room katalin is locked."
   kocinn_katalinwaz_locked = "Room katalinwaz is locked."

   kocinn_rentableroom_door = \
      "The door won't budge.  Strangely, it doesn't look like it opens "
      "at all.  There is a small keyhole in the door, however.  Perhaps if "
      "you used a key in it, something interesting might happen."

   kocnews_god_name = "Designers' News"
   kocnews_god_desc = \
      "Check here for information from the game administrators."

   kocinn_music = song12.ogg

classvars:

   vrName = room_name_kocinn

   viTeleport_row = 4
   viTeleport_col = 5

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP
   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SANCTUARY | ROOM_SAFELOGOFF

properties:

   prRoom = room_kocinn
   piRoom_num = RID_KOC_INN

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   prMusic = kocinn_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      // To Kocatan.
      plExits = Cons([ 2, 5, RID_KOCATAN, 28, 7, ROTATE_NONE ],plExits);

      // To Kocatan tavern.
      plExits = Cons([ 12, 8, RID_KOC_TAVERN, 16, 17, ROTATE_NONE ],plExits);

      plExits = Cons([ 5, 2, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      plExits = Cons([ 6, 2, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      plExits = Cons([ 5, 3, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      plExits = Cons([ 6, 3, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);

      plExits = Cons([ 10, 3, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      plExits = Cons([ 10, 4, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);

      plExits = Cons([ 10, 6, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      /// 10,7 finecol is less than 32 has to be hardcoded

      plExits = Cons([ 10, 8, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      /// 11,8 finerow is less than 32 has to be hardcoded

      plExits = Cons([ 7, 6, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      /// 7, 5 finecol is greater than 32 has to be hardcoded

      plExits = Cons([ 8, 6, ROOM_LOCKED_DOOR, kocinn_rentableroom_door],plExits);
      /// 9, 6 finerow less than 32 has to be hardcoded

      propagate;
   }

   SomethingTryGo(what=$,row=0,col=0,fine_row=0,fine_col=0)
   {
      if (row=10) AND (col=7) AND (fine_col < 32)
      {
         Send(what,@MsgSendUser,#message_rsc=kocinn_rentableroom_door);

         return TRUE;
      }

      if (row=11) AND (col=8) AND (fine_row < 32)
      {
         Send(what,@MsgSendUser,#message_rsc=kocinn_rentableroom_door);

         return TRUE;
      }

      if (row=7) AND (col=5) AND (fine_col > 32)
      {
         Send(what,@MsgSendUser,#message_rsc=kocinn_rentableroom_door);

         return TRUE;
      }

      if (row=9) AND (col=6) AND (fine_row < 32)
      {
         Send(what,@MsgSendUser,#message_rsc=kocinn_rentableroom_door);

         return TRUE;
      }

      propagate;
   }

   CreateStandardObjects()
   {
      local lObjCoords1, oNews;

      oNews = Create(&NewsLink,#nid=NID_ANNOUNCEMENTS,
                     #name=kocnews_god_name,#desc=kocnews_god_desc);
      Send(self,@NewHold,#what=oNews,#new_row=4,#new_col=6,
            #fine_row=8,#fine_col=56);

      lObjCoords1 = [ [02,09,08,16,OO_JUG],[12,04,48,08,OO_JUG],
                      [11,09,40,16,OO_GREY_JUG] ];

      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);

      Send(self,@NewHold,#what=Create(&KocatanInnkeeper),
            #new_row=5,#new_col=6,#fine_row=48,#fine_col=56,
            #new_angle=ANGLE_EAST);

      // Show a Marketplace sign in AERIE GUESTHOUSE kocinn.roo
      Send(self,@NewHold,#what=Create(&Store),
           #new_row=2,#new_col=8,#fine_row=46,#fine_col=38,#new_angle=ANGLE_WEST);
           
      propagate;
   }

   EnterRentableRoom(who=$)
   "Sees if we are in the proper location to enter a rentable room in this inn."
   {
      local iRow, iCol;

      iRow = Send(who,@GetRow);
      iCol = Send(who,@GetCol);
      
      // coords: [5,3],[7,5],[9,6],[10,4],[10,7],[11,8]
      if (iRow = 10
            AND (iCol = 3
               OR iCol = 4
               OR iCol = 6
               OR iCol = 7
               OR iCol = 8))
         OR ((iRow = 5
               OR iRow = 6)
            AND (iCol = 2
               OR iCol = 3))
         OR (iRow = 7
              AND (iCol = 5
                  OR iCol = 6))
         OR ((iRow = 8
               OR iRow = 9)
            AND (iCol = 6))
         OR (iRow = 11
              AND (iCol = 8))
      {
         return TRUE;
      }

      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
