// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Lupogg is Monster

constants:

   include blakston.khd

resources:

   include lupogg.lkod

   lupogg_koc_name_rsc = "tez"
   lupogg_name_rsc = "lupogg"
   lupogg_icon_rsc = lupogg.bgf
   lupogg_desc_rsc = \
      "This underwater denizen, although hideous and brutish, is rumored "
      "to be peaceful in nature, but there is no evidence of that now.  The "
      "lupoggs seem upset that their territory has been trespassed upon, "
      "and seem eager to challenge any they can find."

   lupogg_dead_icon_rsc = lupoggX.bgf
   lupogg_dead_name_rsc = "dead lupogg"

   lupogg_sound_hit = lup_atkh.ogg
   lupogg_sound_miss = lup_atkm.ogg
   lupogg_sound_death = lup_dth.ogg
   lupogg_sound_aware = lup_awr.ogg

classvars:

   vrKocName = lupogg_koc_name_rsc
   vrName = lupogg_name_rsc
   vrIcon = lupogg_icon_rsc
   vrDesc = lupogg_desc_rsc
   vrDead_icon = lupogg_dead_icon_rsc
   vrDead_name = lupogg_dead_name_rsc

   viTreasure_type = TID_POGG
   viAttack_type = ATCK_WEAP_BITE
   viSpeed = 4
   viAttributes = 0
   viLevel = 125
   viDifficulty = 9
   viAmountofLoot = 6
   viVisionDistance = 18
   // Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viKarma = 100
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_WIZARD_KILLER
   viCashmin = 500
   viCashmax = 1200
   vrSound_hit = lupogg_sound_hit
   vrSound_miss = lupogg_sound_miss
   vrSound_aware = lupogg_sound_aware
   vrSound_death = lupogg_sound_death

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_COLD, 90],
                        [-ATCK_SPELL_SHOCK, 90],
                        [-ATCK_SPELL_ACID, 10 ],
                        [-ATCK_SPELL_FIRE, 10 ],
                        [ATCK_WEAP_SLASH, 55 ],
                        [ATCK_WEAP_NERUDITE, 25 ],
                        [ATCK_WEAP_PIERCE, 70 ],
                        [ATCK_WEAP_THRUST, 70 ],
                        [ATCK_WEAP_BLUDGEON, 30 ]];

      propagate;
   }

   MonsterAttack(what=$)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,2,2,5);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,2,2,5);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,100,2,6,2,8,2,1);

         return;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////