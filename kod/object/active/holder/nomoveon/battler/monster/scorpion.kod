// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Scorpion is Monster

constants:

   include blakston.khd

resources:

   include scorpion.lkod

   scorpion_koc_name_rsc = "kinkauikinich"
   scorpion_name_rsc = "giant scorpion"
   scorpion_icon_rsc = scorp.bgf
   scorpion_desc_rsc = \
      "The scorpion's deadly stinger rises high in the air "
      "and its hard plated body protects it from all but the "
      "strongest attacks."
   scorpion_illusion_poisoned = \
      "%s%s reels as poison from your attack courses through %s veins."

   scorpion_dead_icon_rsc = scorpX.bgf
   scorpion_dead_name_rsc = "dead scorpion"

   scorpion_sound_hit = scp_atkh.ogg
   scorpion_sound_miss = scp_atkm.ogg
   scorpion_sound_death = scp_dth.ogg
   scorpion_sound_aware = scp_awr.ogg

classvars:

   vrKocName = scorpion_koc_name_rsc
   vrName = scorpion_name_rsc
   vrIcon = scorpion_icon_rsc
   vrDesc = scorpion_desc_rsc
   vrDead_icon = scorpion_dead_icon_rsc
   vrDead_name = scorpion_dead_name_rsc

   viTreasure_type = TID_MEDIUM_TOUGH
   viSpeed = SPEED_FAST
   viAttack_type = ATCK_WEAP_STING

   viLevel = 65
   viDifficulty = 8
   viAmountofLoot = 4
   viVisionDistance = 10
   // Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viKarma = 60
   viWimpy = 40
   viCashmin = 275
   viCashmax = 650
   vrSound_hit = scorpion_sound_hit
   vrSound_miss = scorpion_sound_miss
   vrSound_aware = scorpion_sound_aware
   vrSound_death = scorpion_sound_death

properties:

   piPoison_Chance = 18
   piPoison_Duration = 180000
   piPoison_Loss_Rate = 3500
   
   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_ACID, 60 ],
                        [ATCK_WEAP_SLASH, 60 ],
                        [ATCK_WEAP_BLUDGEON, -10 ] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,2,2,3);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,4,2,6,2,1);

         return;
      }

      propagate;
   }

   HitSideEffect(what=$,who=$)
   {
      local oSpell;

      if Random(1,piPoison_Chance) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=piPoison_Loss_Rate,#duration=piPoison_Duration);

         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=scorpion_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
