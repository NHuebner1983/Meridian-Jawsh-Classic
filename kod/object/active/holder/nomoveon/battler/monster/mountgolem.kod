// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
MountainGolem is Monster

constants:

   include blakston.khd

   ANIM_CAST = 2

   FRAME_STAND = 1
   FRAME_WALK_START = 2
   FRAME_WALK_END = 6
   FRAME_ATTACK_START = 7
   FRAME_ATTACK_END = 10
   FRAME_CAST_START = 11
   FRAME_CAST_END = 14

resources:
   
   MountainGolem_name_rsc = "mountain golem"
   MountainGolem_icon_rsc = mountgolem.bgf
   MountainGolem_desc_rsc = \
      "A large rock at first glance, crumbling and cracking can be heard "
      "as it stands and begins to persue you. "
   
   MountainGolem_dead_icon_rsc = mountgolemX.bgf
   MountainGolem_dead_name_rsc = "dead mountain golem"

   MountainGolem_sound_hit = elem_atkh.ogg
   MountainGolem_sound_miss = elem_atkm.ogg
   MountainGolem_sound_death = elem_dth.ogg
   MountainGolem_sound_aware = elem_awr.ogg

classvars:

   vrName = MountainGolem_name_rsc
   vrIcon = MountainGolem_icon_rsc
   vrDesc = MountainGolem_desc_rsc
   vrDead_icon = MountainGolem_dead_icon_rsc
   vrDead_name = MountainGolem_dead_name_rsc

   viTreasure_type = TID_NONE
   viSpeed = SPEED_AVERAGE
   viAttributes = 0
   viLevel = 200
   viDifficulty = 8
   viVisionDistance = 28

   
   viAttackRange = 192

   viKarma = 0
   viDefault_behavior = AI_FIGHT_AGGRESSIVE
   viCashmin = 1100
   viCashmax = 2000
   vrSound_hit = MountainGolem_sound_hit
   vrSound_miss = MountainGolem_sound_miss
   vrSound_aware = MountainGolem_sound_aware
   vrSound_death = MountainGolem_sound_death

properties:

   piAnimation = ANIM_NONE

   piSpellPower = 99

messages:

   Constructed()
   {
      if plResistances = $
      {
         plResistances = [ [-ATCK_SPELL_ALL, 90 ],
                           [ATCK_WEAP_ALL, 90 ] ];
      }

      plSpellBook = [ [SID_EARTHQUAKE,5,40] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_ONCE);
      AddPacket(4,250,2,8,2,10,2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,FRAME_WALK_START,2,FRAME_WALK_END);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,225,2,FRAME_ATTACK_START,2,
                   FRAME_ATTACK_END,2,FRAME_STAND);

         return;
      }

      if piAnimation = ANIM_CAST
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,FRAME_CAST_START,2,
                   FRAME_CAST_END,2,FRAME_STAND);

         return;
      }

      propagate;
   }

   MonsterCastAnimation(iSpellNum=$)
   "If a monster has a special cast animation, call this."
   {
      if iSpellNum = SID_EARTHQUAKE
      {
         piAnimation = ANIM_CAST;
      }

      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   CanMorphTo()
   {
      return FALSE;
   }

   SomethingMoved(what=$)
   {
      if what <> self OR poOwner = $
      {
         propagate;
      }

      // Shake the ground when I take a step.
      Send(poOwner,@Rumble,#duration=250,#disruption=25);

      propagate;
   }
   
end
////////////////////////////////////////////////////////////////////////////////
