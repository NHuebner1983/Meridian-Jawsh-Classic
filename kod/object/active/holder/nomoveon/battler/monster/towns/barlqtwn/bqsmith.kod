// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
BarloqueBlacksmith is BarloqueTown

/// Fehr'loi Qan moved from Tos to Barloque

constants:

   include blakston.khd

   ANIM_WAVE = 1

resources:

   include bqsmith.lkod

   BarloqueBlacksmith_name_rsc = "Fehr'loi Qan"
   BarloqueBlacksmith_icon_rsc = bqsmith.bgf
   BarloqueBlacksmith_desc_rsc = \
      "Though well known throughout the land for his superb skill as a master "
      "weapon smith, Fehr'loi Qan himself could not be considered the "
      "sharpest sword on the rack.  The large man's slow speech seems out of "
      "place in the highbrow city of Barloque.  You get the impression that "
      "he is uncomfortable with strangers, but his great size and clear "
      "devotion to Kraanan makes him afraid of no one."

   BQsmith_entry_welcome = "Hello %s%s, and welcome."
   BQSm_mace_sale = \
      "All these beautiful weapons, and you want %s%s.  Son of a ...!  "
      "Well, I suppose I got no choice but to sell you one."
   BQsmith_already_have_spell = \
      "You already have knowledge of the %s spell.  You waste my time."
   BQsmith_not_ready = \
      "Your not ready to learn how to craft that.  "

classvars:

   vrName = BarloqueBlacksmith_name_rsc
   vrIcon = BarloqueBlacksmith_icon_rsc
   vrDesc = BarloqueBlacksmith_desc_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_NOMOVE | MOB_LISTEN \
      | MOB_BUYER | MOB_TEACHER | MOB_COND_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_BLACKSMITH
   viMerchant_markup = MERCHANT_EXPENSIVE

properties:

messages:

   ObjectDesired(what=$)
   {
      if Send(what,@CanBeGiventoNPC)
         AND (Send(Self,@IsObjectWearable,#what=what)
         OR Send(Self,@IsObjectMisc,#what=what)
         OR Send(Self,@IsObjectWeapon,#what=what))
      {
         return True;
      }

      return False;
   }

   SetForSale()
   {
      plFor_sale = [
         [ 
            Create(&ChainArmor),
            Create(&MetalShield),
            Create(&Axe),
            Create(&Hammer),
            Create(&PlateArmor)
         ],
         $,
         [
            SID_MAGICSHIELD,
            SID_SMELTING,
            SID_CRAFTPLATEARMOR,
            SID_CRAFTGUILDSHIELD
         ],
      $];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&Mace);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,100,BQSm_mace_sale]);

      return;
   }

   UserEntered(who=$)
   {
      Post(who,@SomeoneSaid,#what=self,#string=BQSmith_entry_welcome,
            #type=SAY_RESOURCE,
            #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if sid = SID_CRAFTGUILDSHIELD
      {
         if NOT Send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
         {
            return FALSE;
         }
      }

      propagate;
   }

   CanAddSpell(who=$,num=0,report=FALSE)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }

   SpecialTeachChecks(who=$,num=0,report=FALSE)
   {
      local i, oSkill, oSpell, iSchoolRequired, bFound;

      if who = $ OR num = 0
      {
         Debug("Called SpecialTeachChecks with bad parameters!");

         return FALSE;
      }

      oSpell = Send(SYS,@FindSpellByNum,#num=num);

      if NOT Send(oSpell,@IsAccessible)
      {
         return FALSE;
      }

      if Send(who,@HasSpell,#num=num)
      {
         if report
         {
            Post(self,@SayToOne,#target=who,
                  #message_rsc=BQsmith_already_have_spell,
                  #parm1=Send(oSpell,@GetName));
         }

         return FALSE;
      }

      bFound = FALSE;
      iSchoolRequired=0;
      
      if num = SID_CRAFTGUILDSHIELD
      {
         iSchoolRequired = SKS_FENCING;
      }

      if iSchoolRequired = 0
      {
         return TRUE;
      }

      // Special checks for high level spells: need a high level spell in 
      // a particular school.
      foreach i in Send(who,@GetSkillList)
      {
         oSkill = Send(SYS,@FindSkillByNum,
                        #num=Send(who,@DecodeSkillNum,#compound=i));
         if Send(oSkill,@GetLevel) > 4
            AND Send(oSkill,@GetSchool) = iSchoolRequired
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound = FALSE
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=BQsmith_not_ready);
         }

         return FALSE;
      }

      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
