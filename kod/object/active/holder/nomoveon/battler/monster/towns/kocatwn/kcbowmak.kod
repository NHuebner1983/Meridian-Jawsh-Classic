// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
KocatanBowMaker is KocatanTown

constants:

   include blakston.khd

resources:

   include kcbowmak.lkod

   KocatanBowMaker_name_rsc = "Hanla zax'Ta"
   KocatanBowMaker_icon_rsc = kcbowmak.bgf
   KocatanBowMaker_desc_rsc = \
      "This becoming, young woman appears as cold and curt to new faces.  "
      "Despite that, it is obvious to all that her true passion lies in the "
      "crafting of fine bows."
	  
   KocatanBowMaker_Chalice_sale = \
      "I've been collecting these Chalices of Rain from the caves "
	  "which connect the mainland to the island. I would only part with one "
	  "if the price is right. They give off a strange energy I've only "
	  "sensed in Shal'ille followers. I feel as though their energy could "
	  "take you to somewhere magical. Maybe you can use it to escape "
	  "this god forsaken jungle. It's weird, every time I bring it outside "
	  "it fills with water on its own. It surely has been a nice source "
	  "of drinking water."

   KocatanBowMaker_KriipaClaw_sale = \
      "I have claws from several Kriipa I've killed in the jungle.  The "
      "Kriipa is a truly fearsome beast, as I'm sure you know, and a hunter "
      "who wishes to obtain their claws must pay for them in blood spilt in "
      "battle.  You will understand, then, that my price for them is high."
   KocatanBowMaker_unwanted_give = \
      "~k%s%s tells you, \"I have enough of those type of supplies to hold "
      "us through the summer.~n~k\""

classvars:

   vrName = KocatanBowMaker_name_rsc
   vrIcon = KocatanBowMaker_icon_rsc
   vrDesc = KocatanBowMaker_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM \
      | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE
   viOccupation = MOB_ROLE_BLACKSMITH

   viGender = GENDER_FEMALE

   vrUnwanted_give = KocatanBowMaker_unwanted_give

properties:

messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what,&LongBow)
         {
            Send(self,@SetMood,#new_mood=piMood-2);
         }

         return;
      }

      return;
   }

   UserEntered(who=$)
   {
      if Send(who,@GetGender) = GENDER_MALE
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      if Send(who,@GetGender) = GENDER_FEMALE
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [ 
         [ 
            Create(&PracticeArrow),
            Create(&Arrow),
            Create(&LongBow),
            Create(&PracticeBow),
            Create(&DrawKnife)
         ],
         $,
         [
            SID_CRAFTBATTLEBOW,
            SID_CRAFTLONGBOW
         ],
         $];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&KriipaClaw,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,250,
            KocatanBowMaker_KriipaClaw_sale]);

      oObj=Create(&Chalice);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,1000000,
            KocatanBowMaker_Chalice_sale]);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
