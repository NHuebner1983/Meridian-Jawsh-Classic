// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
CorNothSergeant is CorNothTown

constants:

   include blakston.khd

resources:

   include cnsarge.lkod

   cornothsergeant_name_rsc = "Rook"
   cornothsergeant_icon_rsc = cnsarge.bgf
   cornothsergeant_desc_rsc = \
      "A nobleman and former military officer, Rook carries himself with a "
      "stiff dignity.  He is a weaponsmaster, and makes a living passing his "
      "skills on to students of the warriors' path.  His feats with the "
      "sword are famous and unparalleled as is his love for a good drink."

   CnSer_forget_potion_sale = \
      "Good, no self-respecting warrior shares company with those "
      "mana-crazed buffoons.  I will most gladly sell %s%s that strips all "
      "dependency on the so-called Warrior god from thy memory. ~y~BBuyer beware.~B~y"
   
   CnSer_forget_xp_potion_sale = \
      "So you think you know everything? "
      "I will most gladly sell %s%s that strips all "
      "experience from your being. Be warned though, it is best not to move "
      "or attack anything 10 to 15 seconds before digesting or you may forget "
      "more than expected. Although I can sell this potion to anyone, only those "
      "who have reached their max potential can swallow its contents. ~y~BBuyer beware.~B~y"

   CnSer_Alcohol_taken = \
      "Ohhh, thank you! Here, have some of my old training swords! They "
      "won't last much longer, but maybe you can fix them up?"

   CnSer_Alcohol_toomuch = \
      "I couldn't possibly accept this much alcohol, do you think I have "
      "a drinking problem?"

   CnSer_inv_full_one = \
      "You're carrying too many items already, I'll place the training sword "
      "by your feet."

   CnSer_inv_full_many = \
      "You're carrying too many items already, I'll place the training swords "
      "by your feet."

classvars:

   vrName = cornothsergeant_name_rsc
   vrIcon = cornothsergeant_icon_rsc
   vrDesc = cornothsergeant_desc_rsc

   viMerchant_markup = MERCHANT_BARGAIN

   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_TEACHER \
      | MOB_RECEIVE | MOB_NOMOVE | MOB_SELLER | MOB_COND_SELLER
   viOccupation = MOB_ROLE_SERGEANT

properties:

messages:

   Constructed()
   {
      plWantedItems = [&Mug, &StoutGoblet, &WineGoblet, &KocatanMug, &Goblet];

      propagate;
   }

   UserEntered(who = $)
   {
      if Send(who,@GetFaction) = FACTION_DUKE
      {
         Send(self,@SetMood,#new_mood=piMood-3);
      }

      if Send(who,@GetFaction) = FACTION_PRINCESS
      {
         Send(self,@SetMood,#new_mood=piMood+3);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [
         [
            Create(&RingDefense),
            Create(&LeatherArmor),
            Create(&LongSword),
            Create(&ShortSword),
            Create(&Mace)
         ],
         [
            SKID_DODGE,
            SKID_PUNCH,
            SKID_SLASH,
            SKID_PROFICIENCY_AXE,
            SKID_PROFICIENCY_HAMMER,
            SKID_BRAWLING,
            SKID_PROFICIENCY_SHORTSWORD,
            SKID_PROFICIENCY_MACE,
            SKID_PROFICIENCY_SWORD,
            SKID_PARRY 
         ],
      $,$];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&ForgetPotionKraanan);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,2500,CnSer_forget_potion_sale]);

      oObj=Create(&ForgetPotionExperience);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,1000000,CnSer_forget_xp_potion_sale]);

      return;
   }

   CheckWhyWanted(obj=$,who=$)
   {
      local oWeapon, iAmount, iHits, iIsFull;

      if IsClass(obj,&Food)
         AND Send(obj,@IsBeverage)
      {
         iAmount = Send(obj,@GetNumber);

         // Avoid potential issues with players giving him 1000s of items.
         if iAmount > 20
         {
            Send(self,@SayToOne,#target=who,#message_rsc=CnSer_Alcohol_toomuch);

            return FALSE;
         }

         Send(self,@SayToOne,#target=who,#message_rsc=CnSer_Alcohol_taken);

         // Increment this if player can't carry a sword, will be used
         // to choose which carry-fail message to display.
         iIsFull = 0;

         while (iAmount > 0)
         {
            iHits = Random(1,10);
            oWeapon = Create(&LongSword);
            Send(oWeapon,@SetHits,#number=iHits);

            if Send(who,@ReqNewHold,#what=oWeapon)
            {
               Send(who,@NewHold,#what=oWeapon);
            }
            else
            {
               iIsFull = iIsFull + 1;
               Send(poOwner,@NewHold,#what=oWeapon,#new_row=piRow,#new_col=piCol,
                     #fine_row=piFine_row + 16,#fine_col=piFine_col);
            }

            iAmount = iAmount - 1;
         }

         // If the player couldn't carry any of the swords, give them a message.
         if iIsFull
            AND oWeapon <> $
         {
            if iIsFull = 1
            {
               Send(self,@SayToOne,#target=who,#message_rsc=CnSer_inv_full_one);
            }
            else
            {
               Send(self,@SayToOne,#target=who,#message_rsc=CnSer_inv_full_many);
            }
         }

         // Returning TRUE means GotWantedItem will delete the alcohol.
         return TRUE;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
