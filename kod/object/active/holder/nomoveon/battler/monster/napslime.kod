// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
NapalmSlime is Monster

constants:

   include blakston.khd

resources:

   nap_slime_koc_name_rsc = "napkinachot"
   nap_slime_name_rsc = "napalm slime"
   nap_slime_icon_rsc = amoeba.bgf
   nap_slime_desc_rsc = \
   "A mass of quivering goo, the ooz has a strange "
   "scent stinging my nose.  The smell seems familiar "
   "yet im unable to decipher what it is."

   nap_slime_dead_icon_rsc = amoebaX.bgf

   nap_slime_sound_hit = slm_atkh.wav
   nap_slime_sound_miss = slm_atkm.wav
   nap_slime_sound_death = NapalmSlime_Death.wav
   nap_slime_sound_aware = slm_awr.wav

classvars:

   vrKocName = nap_slime_koc_name_rsc
   vrName = nap_slime_name_rsc
   vrIcon = nap_slime_icon_rsc
   vrDesc = nap_slime_desc_rsc

   viTreasure_type = TID_WIMPY_MEDIUM
   viSpeed = SPEED_SLOW
   viAttack_spell = ATCK_SPELL_ACID
   viAttributes = 0
   viLevel = 35
   viDifficulty = 4
   viAmountofLoot = 2
   viVisionDistance = 10
   viKarma = 0
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SINGLEMINDED
   viCashmin = 1000
   viCashmax = 3000
   vrSound_hit = nap_slime_sound_hit
   vrSound_miss = nap_slime_sound_miss
   vrSound_aware = nap_slime_sound_aware
   vrSound_death = nap_slime_sound_death
   viDead_drawfx = DRAWFX_TRANSLUCENT_75
   
   viBaseBleedPercentage = 20

   viBaseSize = 10

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_SHOCK, 45 ],
                        [-ATCK_SPELL_UNHOLY, 45 ],
                        [-ATCK_SPELL_HOLY, 45 ],
                        [-ATCK_SPELL_ACID, 45 ],
                        [ATCK_WEAP_ALL, 45 ],
                        [ATCK_WEAP_BLUDGEON, 10 ],
                        [-ATCK_SPELL_COLD, -45 ] ];
	  
	  Send(self,@ChangeColor);
      
	  propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,400,2,2,2,5);

      return;
   }

   SendMoveAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      AddPacket(1,ANIMATE_CYCLE,4,180,2,4,2,5);

      return;
   }

   SendAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,2,2,4,2,1);

         return;
      }

      // if no body animation
      propagate;
   }

   ChangeColor()
   {

         piColor_translation = (PT_GRAY_TO_ORANGE);
	  
      if poOwner <> $
      {
         Send(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }
   
   Killed(what=$)
   {
      local oSpell, lFinalTargets, oTrance, oActive, lActive;

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_RING_OF_FLAMES);
      lFinalTargets = Send(oSpell,@GetTargets,#who=self);
      Send(oSpell,@CastSpell,#who=self,#iSpellPower=99,
            #lTargets=lFinalTargets);
			
      oSpell = Send(SYS,@FindSpellByNum,#num=SID_EARTHQUAKE);
      lFinalTargets = Send(oSpell,@GetTargets,#who=self);
      Send(oSpell,@CastSpell,#who=self,#iSpellPower=99,
            #lTargets=lFinalTargets);
  			
	  Send(poOwner,@Rumble,#duration=750,#disruption=100);
	
      propagate;
   }
   
end
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
