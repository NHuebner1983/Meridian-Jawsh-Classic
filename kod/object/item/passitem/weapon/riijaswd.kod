// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
RiijaSword is Weapon

constants:

   include blakston.khd

resources:

   include riijaswd.lkod

   RiijaSword_name_rsc = "sword of Riija"
   RiijaSword_icon_rsc = quswrd.bgf
   RiijaSword_desc_rsc = \
      "This is a very awkward looking sword, but it feels surprisingly light "
      "in your hands.  Before your very eyes, dark and weird runes shift "
      "slightly on this deadly weapon."
   RiijaSword_desc_append = \
      "  Some of the runes say, \"Let %q walk with faith.\""
   RiijaSword_window_overlay_rsc = povquswd.bgf
   RiijaSword_player_overlay = quswrdov.bgf

classvars:

   vrName = RiijaSword_name_rsc
   vrIcon = RiijaSword_icon_rsc
   vrDesc = RiijaSword_desc_rsc
   viHits_init_min = 600
   viHits_init_max = 650

   // Riija Swords are high quality thrusting weapons
   viWeaponType = WEAPON_TYPE_THRUST
   viWeaponQuality = WEAPON_QUALITY_HIGH
   viProficiency_needed = SKID_PROFICIENCY_SHORT_SWORD
   viValue_average = 5000
   viWeight = 90
   viBulk = 90
   vrWeapon_window_overlay = RiijaSword_window_overlay_rsc
   vrWeapon_window_attack_start = 1
   vrWeapon_window_attack_end = 4
   vrWeapon_window_hold = 5
   vrWeapon_overlay = RiijaSword_player_overlay
   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

   viRange = 3 * FINENESS

properties:

   piAttack_type = ATCK_WEAP_MAGIC+ATCK_WEAP_SLASH
   poQuester = $

messages:

   Constructor(quester=$)
   {
      if quester <> $
      {
         poQuester = quester;
      }

      propagate;
   }

   DropOnDeath()
   {
      return Send(SETTINGS_OBJECT,@GetRiijaSwordDropOnDeath);
   }

   AppendDesc()
   {
      if poQuester <> $
      {
         AddPacket(4,RiijaSword_desc_append,6,Send(poQuester,@GetTrueName));
      }
      else
      {
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   GetQuester()
   {
      return poQuester;
   }

   SetMaster(quester=$)
   "Alias in case we forgot."
   {
      return Send(self,@SetQuester,#quester=quester);
   }

   SetQuester(quester=$)
   "Sets the owner of the sword."
   {
      poQuester = quester;

      return;
   }

   ForgetQuester(quester=$)
   "Sent to all swords when someone suicides."
   {
      if poQuester = quester
      {
         poQuester = $;
      }

      return;
   }

   DestroyDisposable()
   {
      return FALSE;
   }

   Delete()
   {
      poQuester = $;

      propagate;
   }

   CanBeStoredInVault()
   {
      if poOwner <> $
         AND poQuester <> $
      {
         return (poOwner = poQuester);
      }

      return FALSE;
   }

   ModifyHitRoll(hit_roll = 0, target = $)
   "Fairly worthless to hit with except for original quester."
   {
      if poOwner <> $
         AND poQuester <> $
         AND poOwner <> poQuester
         AND NOT IsClass(poOwner, &DM)
      {
         // Return a big number, since normally chance to-hit goes up to 1000.
         return -10000;
      }

      propagate;
   }

   WeaponHitTarget()
   "Sword wears down rapidly in the hands of someone else, but does not break. Owner never loses hits."
   {
      if poOwner = poQuester
      {
         piHits = Send(self,@GetMaxHits);

         return;
      }

      if poOwner <> poQuester
         AND piHits > 1
      {
         piHits = piHits - 1;

         return;
      }

      propagate;
   }

   DoShatter()
   "Do something special when we're shattered."
   {
      local cPiece, lPieces, oPiece, cBadPiece;

      // When shattered, pick 3 random quest pieces and give them to our owner.
      lPieces = [&SwordShardA,&SwordShardB,&SwordShardC,&SwordShardD];
      cBadPiece = Nth(lPieces,random(1,4));

      foreach cPiece in lPieces
      {
         if cPiece = cBadPiece
         {
            continue;
         }

         oPiece = create(cPiece,#quester=poQuester);
         Send(poOwner,@NewHold,#what=oPiece);
      }

      // We are deleted further up the chain.
      propagate;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      if Send(oSpell,@GetSchool) = SS_RIIJA
      {
         // Casting spells while the sword is wielded "heals" the sword.
         piHits = Bound(piHits + Send(oSpell,@GetLevel),1,Send(self,@GetMaxHits));

         return 25;
      }

      return -5;
   }

end
////////////////////////////////////////////////////////////////////////////////
