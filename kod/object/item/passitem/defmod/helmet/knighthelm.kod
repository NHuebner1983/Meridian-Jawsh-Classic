// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
KnightsHelm is Helmet

constants:

   include blakston.khd

resources:

   KnightHelm_name_rsc = "knights helm"
   KnightHelm_icon_male_rsc = khelm.bgf
   KnightHelm_icon_female_rsc = khelmb.bgf
   KnightHelm_desc_rsc = \
      "A helm fit for a knight."   

classvars:

   vrName = KnightHelm_name_rsc
   vrDesc = KnightHelm_desc_rsc
   vrIcon = KnightHelm_icon_male_rsc

   viHits_init_min = 220
   viHits_init_max = 400

   viValue_average = 300
   viWeight = 50
   viBulk = 30

   viUse_type = ITEM_USE_HEAD
   viUse_amount = 1
   viSpell_modifier = -15

   viGround_group = 3
   viInventory_group = 2
   viBroken_group = 4

   viDefense_base = 10
   viDamage_base = 4

properties:


messages:

   NewUsed()
   {
      Send(poOwner,@RemoveHair,#what=self);

      propagate;
   }

   NewUnused()
   {
      Send(poOwner,@ResetHair,#what=self);

      propagate;
   }

   GetResistanceModifiers()
   {
      return [ [ATCK_WEAP_BLUDGEON,-15],
               [ATCK_WEAP_SLASH,5]
             ];
   }
   
   // Called by player, for normal 3rd-person overlays on user.
   GetOverlay(animation = $)
   {
      if Send(poOwner,@GetGender) = GENDER_FEMALE
      {
         return KnightHelm_icon_female_rsc;
      }

      return KnightHelm_icon_male_rsc;
   }
   
   GetHeatDamage()
   {
      return 2;
   }

end
////////////////////////////////////////////////////////////////////////////////
