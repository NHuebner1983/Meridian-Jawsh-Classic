// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
RobeBase is Armor

/// Robe is the first item that takes two different use positions.
/// Note that a robe forcibly unuses anything already there.

constants:

   include blakston.khd

resources:

   include robebase.lkod

   robe_name_rsc = "robes"
   robe_icon_rsc = robe.bgf
   robe_desc_rsc = \
      "Heavy and warm, these full length robes generally denotes one's "
      "dedication to the higher arts of knowledge, spell craft or religion."

   robe_male_icon_rsc = btc.bgf
   robe_female_icon_rsc = btd.bgf

   robe_legs_male = bfc.bgf
   robe_legs_female = bfd.bgf

   robe_leftarm_male = blc.bgf
   robe_rightarm_male =brc.bgf
   robe_leftarm_female = bld.bgf
   robe_rightarm_female =brd.bgf

   robe_condition_exc = " are in immaculate condition."
   robe_condition_exc_mended = \
      " are in excellent condition, but have been patched before.."
   robe_condition_good = " are scuffed and slightly worn."
   robe_condition_med = " have a few unsightly rips."
   robe_condition_poor = " are in tatters, and barely holding together."
   robe_condition_broken = " are covered in filth and ripped beyond use."

classvars:

   vrName = robe_name_rsc
   vrIcon = robe_icon_rsc
   vrDesc = robe_desc_rsc

   vrBroken_rsc = defmod_broken_plural_clothes
   vrItem_broken = item_broken_battle_plural

   viHits_init_min = 550
   viHits_init_max = 650

   viValue_average = 400
   viWeight = 100
   viBulk = 150

   viGround_group = 1
   viInventory_group = 2
   viBroken_group = 3

   viSpell_modifier = 10
   viDefense_base = 20

   viUse_type = ITEM_USE_BODY | ITEM_USE_LEGS
   viItem_type = ITEMTYPE_ARMOR

   vrIcon_male = robe_male_icon_rsc
   vrIcon_female = robe_female_icon_rsc

   vrLeftarm_male = robe_leftarm_male 
   vrRightarm_male = robe_rightarm_male 
   vrLeftarm_female = robe_leftarm_female 
   vrRightarm_female = robe_rightarm_female 

   vrLegs_male = robe_legs_male 
   vrLegs_female = robe_legs_female 

   vrPoss_article = object_article_cap_these_rsc

   vrCondition_exc = robe_condition_exc 
   vrCondition_exc_mended = robe_condition_exc_mended 
   vrCondition_good = robe_condition_good 
   vrCondition_med = robe_condition_med 
   vrCondition_poor = robe_condition_poor 
   vrCondition_broken = robe_condition_broken

   // Robes go over layer 0 items, such as shirts and pants
   viLayer = 1

properties:

   pbColorSet = FALSE

messages:

   Constructor(translation = $,model = $, color = $)
   {
      if color <> $
      {
         translation = Send(SYS,@EncodeTwoColorXLAT,#color1=color,
                           #color2=XLAT_TO_SKIN1);
      }

      if translation <> $
      {
         Send(self,@SetPaletteTranslation,#translation=translation);

         propagate;
      }

      if model <> $
      {
         Send(self,@SetPaletteTranslation,
               #translation=Send(model,@GetPaletteTranslation));

         propagate;
      }
      
	  if NOT pbColorSet
	  {
         Send(self,@SetPaletteTranslation,
               #translation=Send(SYS,@EncodeTwoColorXLAT,#color1=XLAT_TO_BLUE,
                                 #color2=XLAT_TO_SKIN1));
      }

      propagate;
   }

   ReqUse()
   {
      return (NOT Send(Send(SYS,@GetWarEvent),@IsActive));
   }

   HasSkinTones()
   {
      return TRUE;
   }

   ItemChangesArms()
   {
      return 2;
   }

   GetResistanceModifiers()
   {
      return [ [ ATCK_WEAP_ALL, -10],
               [-ATCK_SPELL_FIRE,-10]
             ];
   }

end
////////////////////////////////////////////////////////////////////////////////
