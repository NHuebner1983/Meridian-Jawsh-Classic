// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
ItemAttDurable is ItemAttribute

///////////////////////////////////////////////////////////////////////
//
//  This item gets it's max_hit_points multiplied by three, at the 
//  point when the item is first 'enchanted' (usually on or near
//  the creation of the object.
//
//  Another effect can change the durability factor, by giving a value
//  for state1 when the itematt is applied.  If none is given, the
//  default of 300 is used (or three times the current Hp of the 
//  item).   Note that this value is not stored, because this value
//  is completely unimportant after the value of max_hit_points 
//  changes.
//
//  Form is:
//
//      [IA_DURABLE]
//

///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include iadurabl.lkod

   itematt_durable_name = "well-crafted %s"
   durable_dm = "durable"
   itematt_durable_desc = \
      "  The craftsmanship of this %s is admirable, obviously the result of "
      "hundreds of hours of work by expert hands. You should carefully maintain "
      "this item with a skillful mender. You wouldn't want to break such a unique item."

classvars:

   viItem_Att_Num = IA_DURABLE
   vrName = itematt_durable_name
   vrDesc = itematt_durable_desc
   viDefault_bonus = 300    //// 300 percent bonus to enchanted item.
   vrDM_Trigger = durable_DM

properties:

   piValue_modifier = 150
   piValue_power_modifier = 0

messages:

   AddEffects(oItem=$,state1=$)
   {
      local iHits;

      if state1=$
      {
         state1 = viDefault_Bonus;
      }

      iHits = Send(oItem,@GetMaxHits);
      iHits = (iHits * state1)/100;
      iHits = bound(iHits,1,$);
      Send(oItem,@setMaxHits,#number=iHits);
      Send(oItem,@SetHits,#number=Send(oItem,@GetMaxHits));

      return;
   }

   AppendDesc(oItem =$)
   {
      AddPacket(4,vrDesc, 4,Send(oItem,@GetName));

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
