// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
KocSoldierQuest is SoldierQuestTemplate
// Quest nodes 214, 215, 216.
// QNT_ID_KOC_SOLDIER_ONE, QNT_ID_KOC_SOLDIER_TWO, QNT_ID_KOC_SOLDIER_THREE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:
   
   // TODO: Tyrant translate
   //include kocsoldierqt.lkod

   // Quest description.
   kocsoldier_icon_rsc = flagkinv.bgf
   kocsoldier_name_rsc = "ko'catan soldier shield quest"
   kocsoldier_desc_rsc = \
     "Cital ko'Manca will allow you to join the Ko'catan army, but first "
      "you must prove your loyalty by slaying his enemies."

   kocsoldier_completed_rsc = \
      "You have proven your worth, and are now a soldier in the Ko'catan army."

   // Quest resources.
   koc_faction_soldier_assign_1 =\
      "I have faith in you.  Go dispatch %INDEF_MONSTER%MONSTER somewhere "
      "on the island, return to report on your battle, and then we will talk "
      "about your future as a soldier."
   koc_faction_soldier_assign_2 =\
      "Yes, I have heard stories of your fight.  Now go conquer "
      "%INDEF_MONSTER%MONSTER and you shall join us as an equal "
      "when you are done."
   koc_faction_soldier_success =\
      "I knew you were a fit soldier.  Now you properly join us in "
      "our fight against the mainlanders!"
   koc_faction_soldier_failure =\
      "I am disappointed in you.  I expected better out of a loyal "
      "supporter.  Should you be willing to fight against those that "
      "oppose Ko'catan, you know where to find me."

classvars:

   vrIcon = kocsoldier_icon_rsc
   vrName = kocsoldier_name_rsc
   vrDesc = kocsoldier_desc_rsc

   vrCompleted = kocsoldier_completed_rsc

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_KOC_SOLDIER

properties:

   piPlayerRestrict = \
      Q_PLAYER_FACTION_KOC | Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_PVP_ENABLED

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_KOC_SOLDIER_ONE, QNT_ID_KOC_SOLDIER_TWO,
                      QNT_ID_KOC_SOLDIER_THREE];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #214 is 1st Koc Soldier Shield quest, say "soldier"
      // QNT_ID_KOC_SOLDIER_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[faction_soldier_trigger],
               #quest_node_index=QNT_ID_KOC_SOLDIER_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_KOC_SOLDIER_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                        #iJob=MOB_ROLE_ROYALTY,#cNPC_class=&KocLiege));
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_SOLDIER_ONE");
      }

      // Template #215 is 2nd Koc Soldier Shield quest, kill first faction soldier
      // QNT_ID_KOC_SOLDIER_TWO
      if Send(oQE,@AddQuestNodeTemplate,#NPC_modifier=QN_NPCMOD_SAME,
               #questnode_type=QN_TYPE_MONSTER,#monsterlist=[&AvarShaman],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KOC_SOLDIER_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_SOLDIER_TWO,
               #new_hint=koc_faction_soldier_assign_1);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_SOLDIER_TWO,
               #new_hint=koc_faction_soldier_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_SOLDIER_TWO");
      }

      // Template #216 is 3rd Koc Soldier Shield quest, kill second faction soldier
      // QNT_ID_KOC_SOLDIER_THREE
      if Send(oQE,@AddQuestNodeTemplate,#NPC_modifier=QN_NPCMOD_SAME,
               #questnode_type=QN_TYPE_MONSTER,#monsterlist=[&AvarChieftain],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &KocShield, 1 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KOC_SOLDIER_THREE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_SOLDIER_THREE,
               #new_hint=koc_faction_soldier_assign_2);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_KOC_SOLDIER_THREE,
               #new_hint=koc_faction_soldier_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_SOLDIER_THREE,
               #new_hint=koc_faction_soldier_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_SOLDIER_THREE");
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
