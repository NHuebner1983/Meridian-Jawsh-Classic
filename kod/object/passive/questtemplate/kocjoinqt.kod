// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
KocJoinQuest is QuestTemplate
// QST_ID_KOC_JOIN = xx
// Quest nodes xxx, xxx
// QNT_ID_KOC_JOIN_ONE, QNT_ID_KOC_JOIN_TWO

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   // TODO: Tyrant translations
   include kocjoinqt.lkod

   // Quest description.
   kocjoin_icon_rsc = kcguardG.bgf
   kocjoin_name_rsc = "Ko'catan join faction quest"
   kocjoin_desc_rsc = \
      "Before Cital ko'Manca will let you join his ranks, he must be convinced "
      "you are not a mainland traitor. Bring him an item, and you will gain his trust."

   kocjoin_deliver_rsc = \
      "Before Cital ko'Manca will let you join his ranks, he must be convinced "
      "you are not a mainland traitor. Bring him %s%s, and you will gain his trust."

   kocjoin_completed = \
      "You have earnt the trust of Cital ko'Manca, and are now a member "
      "of his faction."

   // Quest resources.
   koc_join2_assign = \
      "You wish to join the fight for Ko'catan?  I cannot trust you, for "
      "you may be a traitor from the mainland.  Prove your value by bringing "
      "me %INDEF_CARGO%CARGO."
   koc_join2_success = \
      "Excellent work!  This will prove to be useful in battle.  "
      "Consider yourself one of the Ko'catan heroes of the island."
   koc_join2_failure = \
      "Subject: Your lack of punctuality.\n"
      "Your lack of actions probably confirms my fears that you were a traitor "
      "from the mainland.  Do not bother me again with your "
      "false desires to fight for the island of Ko'Catan."

classvars:

   vrIcon = kocjoin_icon_rsc
   vrName = kocjoin_name_rsc
   vrDesc = kocjoin_desc_rsc

   vrNodeTwo = kocjoin_deliver_rsc

   vrCompleted = kocjoin_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_KOC_JOIN

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_INTRIGUING | Q_PLAYER_FACTION_NEUTRAL \
                      | Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 30
   piSchedulePct = 100
   piTimeLimit = 3600 // 1 hour.

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_KOC_JOIN_ONE, QNT_ID_KOC_JOIN_TWO];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_KOC_JOIN_TWO, vrNodeTwo] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #199 is 1st Koc Join node, say "join" to Jonas
      // QNT_ID_KOC_JOIN_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[duke_join1_trigger],
               #quest_node_index=QNT_ID_KOC_JOIN_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_KOC_JOIN_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                        #iJob=MOB_ROLE_ROYALTY,#cNPC_class=&KocLiege));
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_JOIN_ONE");
      }

      // Template # 200 is 2nd Koc Join node, bring an item to Jonas
      // QNT_ID_KOC_JOIN_TWO
      if Send(oQE,@AddQuestNodeTemplate,#NPC_modifier=QN_NPCMOD_SAME,
               #questnode_type=QN_TYPE_ITEMFINDCLASS,\
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &NeruditeArmor, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &IvyCirclet, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &NeruditeSword, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &BattleBow, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &Meatpie, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &DragonflyEye, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &TeloFruit, 1 ] ],\
               #prizelist=[ [ QN_PRIZETYPE_FACTION, QN_PRIZE_FACTION_KOC ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_KOC_JOIN_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_JOIN_TWO,
               #new_hint=koc_join2_assign);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_KOC_JOIN_TWO,
               #new_hint=koc_join2_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_JOIN_TWO,
               #new_hint=koc_join2_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_JOIN_TWO");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oCargo;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      // Quest node QNT_ID_KOC_JOIN_TWO dialog needs
      // cargo indef article and name.
      if index = QNT_ID_KOC_JOIN_TWO
      {
         oCargo = Send(oQuestNode,@GetCargo);
         AddPacket(4,Send(oCargo,@GetIndef), 4,Send(oCargo,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
