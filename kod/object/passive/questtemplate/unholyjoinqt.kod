// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
UnholyJoinQuest is QuestTemplate
// QST_ID_UNHOLY_JOIN = xx
// Quest nodes xxx, xxx
// QNT_ID_UNHOLY_JOIN_ONE, QNT_ID_UNHOLY_JOIN_TWO

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:
   
   // TODO: Tyrant translations
   //include unholyjoinqt.lkod

   // Quest description.
   unholyjoin_icon_rsc = licha.bgf
   unholyjoin_name_rsc = "necro army join faction quest"
   unholyjoin_desc_rsc = \
      "Before Queen Venya'Cyr will let you join her ranks, she must be convinced "
      "you are not one of Shal'ille's infiltrators. Bring her an item, and you will gain her trust."

   unholyjoin_deliver_rsc = \
      "Before Queen Venya'Cyr will let you join her ranks, she must be convinced "
      "you are not affiliated with Shal'ille. Bring her %s%s, and you will gain her trust."

   unholyjoin_completed = \
      "You have earnt the trust of Queen Venya'Cyr, and are now a minion "
      "of necromancer army."

   // Quest resources.
   unholy_join2_assign = \
      "You wish to join the fight against our holy enemies?  Prove you're not a worm and bring "
      "me %INDEF_CARGO%CARGO."
   unholy_join2_success = \
      "Excellent work!  This will prove to be useful in battle.  "
      "Consider yourself one of the Necromancers."
   unholy_join2_failure = \
      "Subject: Your lack of evilness.\n"
      "Your lack of actions probably confirms my fears that you are just another one of "
      "Shal'ille's holy inquisitors. Be gone, and take your "
      "insolence with you."

classvars:

   vrIcon = unholyjoin_icon_rsc
   vrName = unholyjoin_name_rsc
   vrDesc = unholyjoin_desc_rsc

   vrNodeTwo = unholyjoin_deliver_rsc

   vrCompleted = unholyjoin_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_UNHOLY_JOIN

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_INTRIGUING | Q_PLAYER_FACTION_NEUTRAL \
                      | Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 30
   piSchedulePct = 100
   piTimeLimit = 3600 // 1 hour.

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_UNHOLY_JOIN_ONE, QNT_ID_UNHOLY_JOIN_TWO];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_UNHOLY_JOIN_TWO, vrNodeTwo] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #199 is 1st Koc Join node, say "join" to Jonas
      // QNT_ID_UNHOLY_JOIN_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[duke_join1_trigger],
               #quest_node_index=QNT_ID_UNHOLY_JOIN_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_UNHOLY_JOIN_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                        #iJob=MOB_ROLE_ROYALTY,#cNPC_class=&UnholyLiege));
      }
      else
      {
         Debug("Error building QN: QNT_ID_UNHOLY_JOIN_ONE");
      }

      // Template # 200 is 2nd Koc Join node, bring an item to Jonas
      // QNT_ID_UNHOLY_JOIN_TWO
      if Send(oQE,@AddQuestNodeTemplate,#NPC_modifier=QN_NPCMOD_SAME,
               #questnode_type=QN_TYPE_ITEMFINDCLASS,\
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &DiscipleRobe, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &OrcShield, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &MysticSword, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &DaemonHelm, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &IvyCirclet, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &PurpleMushroom, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &RedMushroom, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &EntrootBerry, 1 ],\
                            [ QN_PRIZETYPE_ITEMCLASS, &InkyCap, 1 ] ],\
               #prizelist=[ [ QN_PRIZETYPE_FACTION, QN_PRIZE_FACTION_UNHOLY ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_UNHOLY_JOIN_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_UNHOLY_JOIN_TWO,
               #new_hint=unholy_join2_assign);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_UNHOLY_JOIN_TWO,
               #new_hint=unholy_join2_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_UNHOLY_JOIN_TWO,
               #new_hint=unholy_join2_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_UNHOLY_JOIN_TWO");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oCargo;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      // Quest node QNT_ID_UNHOLY_JOIN_TWO dialog needs
      // cargo indef article and name.
      if index = QNT_ID_UNHOLY_JOIN_TWO
      {
         oCargo = Send(oQuestNode,@GetCargo);
         AddPacket(4,Send(oCargo,@GetIndef), 4,Send(oCargo,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
