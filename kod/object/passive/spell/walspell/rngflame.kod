// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
RingOfFlames is WallSpell

constants:

   include blakston.khd

resources:

   include rngflame.lkod

   RingOfFlames_name_rsc = "ring of flames"
   RingOfFlames_icon_rsc = iringfir.bgf
   RingOfFlames_desc_rsc = \
      "Creates a ring of flames around target.  "
      "Requires red mushrooms and firesand to cast."

   RingOfFlames_caster = "A blazing hot ring of flames appears around %s%s."
   RingOfFlames_target = "A blazing hot ring of flames appears around you!"
   RingOfFlames_failed_rsc = \
      "There is too much summoning magic focused here to create a ring of "
      "flames." 

   RingOfFlames_sound = frngfir.ogg

classvars:

   vrName = RingOfFlames_name_rsc
   vrIcon = RingOfFlames_icon_rsc
   vrDesc = RingOfFlames_desc_rsc

   viSpell_num = SID_RING_OF_FLAMES
   viSchool = SS_FAREN
   viSpell_level = 5
   viMana = 15
   viSpellExertion = 3
   viMeditate_ratio = 50

   vrSucceed_wav = RingOfFlames_sound

   viHarmful = TRUE

   // We technically have 12 elements, but that would never let us cast this while
   //  another wall spell is active.  A little cheat here, only count 9 elements.
   viNumElements = 9
   vrSummonLimitMsg = RingOfFlames_failed_rsc
   // If TRUE, can target others with this spell.
   vbCanCastOnOthers = FALSE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RedMushroom,3],plReagents);
      plReagents = Cons([&Firesand,3], plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return vbCanCastOnOthers;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=1)
   {
      local oTarget;

      if (lTargets = $
         OR NOT vbCanCastOnOthers)
      {
         oTarget = who;
      }
      else
      {
         oTarget = First(lTargets);
      }

      if oTarget = $
      {
         Debug("Ring of Flames cast with nil target");

         return;
      }

      Send(oTarget,@MsgSendUser,#message_rsc=RingOfFlames_target);

      if (oTarget <> who)
      {
         Send(who,@MsgSendUser,#message_rsc=RingOfFlames_caster,
               #parm1=Send(oTarget,@GetDef),#parm2=Send(oTarget,@GetName));
      }

      propagate;
   }

   PlaceWallElements(who=$,lTargets=$,iSpellPower=1)
   {
      local oRoom, iRow, iCol, iFine_Row, iFine_Col, iXStep, iYStep,
            iAngle, iMaxDamage, iDuration, iHalfrow, iHalfCol, iHalfFine_Row,
            iHalfFine_Col, oElement, oTarget;

      if (lTargets = $
         OR NOT vbCanCastOnOthers)
      {
         oTarget = who;
      }
      else
      {
         oTarget = First(lTargets);
      }

      iAngle = Send(oTarget,@GetAngle);
      iMaxDamage = iSpellPower / 5;
      iMaxDamage = Bound(iMaxDamage,1,20);
      iDuration = (iSpellPower * 2) + 30;
      iDuration = Bound(iDuration,30,180);

      oRoom = Send(oTarget,@GetOwner);

      iRow = Send(oTarget,@GetRow);
      iCol = Send(oTarget,@GetCol);

      // Create fire in eight cardinal directions
      oElement = Create(&WallofFire,#Caster=who,#MaxDamage=iMaxDamage,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow+1),#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow+1),#new_col=iCol,
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow+1),#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow-1),#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow-1),#new_col=iCol,
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oElement = Create(&WallofFire,#MaxDamage=iMaxDamage,#Caster=who,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oElement,#new_row=(iRow-1),#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);


      // Create four "filler" elements in corners.
      oElement = Create(&PassiveWallofFire,#Caster=who,#MaxDamage=iMaxDamage,
                        #Duration=iDuration,#Illusionary=FALSE);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=iCol,
           #fine_row=0,#fine_col=0);

      oElement = Create(&PassiveWallofFire,#Caster=who,#MaxDamage=iMaxDamage,
                        #Duration=iDuration,#Illusionary=FALSE);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=iCol,
           #fine_row=0,#fine_col=FINENESS);

      oElement = Create(&PassiveWallofFire,#Caster=who,#MaxDamage=iMaxDamage,
                        #Duration=iDuration,#Illusionary=FALSE);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=iCol,
           #fine_row=FINENESS,#fine_col=0);

      oElement = Create(&PassiveWallofFire,#Caster=who,#MaxDamage=iMaxDamage,
                        #Duration=iDuration,#Illusionary=FALSE);
      Send(oRoom,@NewHold,#what=oElement,#new_row=iRow,#new_col=iCol,
           #fine_row=FINENESS,#fine_col=FINENESS);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
