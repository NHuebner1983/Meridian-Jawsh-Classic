// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
QorBane is RoomEnchantment

constants:

   include blakston.khd

resources:

   include qorbane.lkod

   QorBane_name_rsc = "Qorbane"
   QorBane_icon_rsc = iqorbane.bgf
   QorBane_desc_rsc = \
      "Permeates an area with the holy essence of Shal'ille, harming any who "
      "attempt to call on the evil energy of Qor.  Requires one vial of yrxl "
      "sap and one fairy wing to cast."
   
   QorBane_unnecessary = "This area is already polarized against Qor."

   QorBane_on = \
      "A magical force enters the area, filling the air with a sweet odor.  "
      "Everything around you seems to glow almost imperceptibly, radiating "
      "holy energy."
   QorBane_off = "The holy force dissipates."
   QorBane_new_entrant = \
      "This place is permeated by a holy presence;  you can feel the eyes "
      "of Shal'ille watching you, warning you against calling upon the "
      "magical forces of Qor."

   QorBane_hit_rsc = \
      "As you utter the final syllables of your spell, a wash of holy "
      "power passes through your body, scalding you with purity."
   QorBane_kill_rsc = \
      "As you utter the final syllables of your spell, a wash of holy power "
      "passes through your body.  The wave of purity is too much for you to "
      "stand, and you expire."
   QorBane_killer_rsc = \
      "%s%s expires from the holy power of your Qorbane enchantment."

   QorBane_sound = sqrbane.ogg

classvars:

   vrName = QorBane_name_rsc
   vrIcon = QorBane_icon_rsc
   vrDesc = QorBane_desc_rsc

   vrAlreadyEnchanted = QorBane_unnecessary
   vrRoomEnchKilled = QorBane_kill_rsc
   vrRoomEnchKiller = QorBane_killer_rsc
   vrRoomEnchHit = QorBane_hit_rsc

   viAttack_spell = ATCK_SPELL_HOLY

   viSpell_num = SID_QORBANE
   viSchool = SS_SHALILLE
   viMana = 15
   viSpell_level = 4
   viSpellExertion = 3
   viCast_time = 5000
   viChance_To_Increase = 40
   viMeditate_ratio = 30

   vbIsAreaEffect = TRUE

   vrSucceed_wav = QorBane_sound
   viHarmful = TRUE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Yrxlsap,1],plReagents);
      plReagents = Cons([&FairyWing,1],plReagents);

      return;
   }

   CastSpell(who = $, iSpellPower = 0)
   "Initiation point for the spell."
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);
      if Send(oRoom,@IsEnchanted,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=vrAlreadyEnchanted);

         return;
      }

      // global effects of the enchantment
      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=QorBane_on,#what=self);
      Send(oRoom,@RoomStartEnchantment,#what=self,#state=[iSpellPower,who],
            #time=Send(self,@GetDuration,#iSpellPower=iSpellPower));

      propagate;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = 45 + (iSpellPower * 3);  /// 30 - 228 seconds
      iDuration = iDuration * 1000;      /// convert to seconds

      return Random(iDuration/2,iDuration);
   }

   StartEnchantmentNewOccupant(who = $)
   "Called on new occupants of the enchanted room."
   {
      Send(who,@MsgSendUser,#message_rsc=QorBane_new_entrant);

      return;
   }

   EndSpell(where = $, state = $)
   "Called when spell expires."
   {
      Send(where,@SomeoneSaid,#type=SAY_MESSAGE,#string=QorBane_off,#what=self);

      return;
   }

   ComputeDamage(iSpellPower=$,iLevel=1)
   {
      // Conceptually: ((iSpellPower/15) * (iLevel/2) * Random(8,12))/10
      return (iSpellPower * iLevel * Random(8,12))/300;
   }

end
////////////////////////////////////////////////////////////////////////////////
