// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Brain is PassiveObject

constants:

   include blakston.khd

   TS_MOVE = 1
   TS_FIGHT_ME_VICTIM = 2
   TS_FIGHT_ME_AGGRESSOR = 3
   TS_FIGHT = 4
   TS_MURDER = 5
   TS_ENTRY = 6
   TS_ENTRY_APPROVED = 7

resources:

classvars:

   viBrain_num = BRAIN_ORIGINAL

properties:

   piDefault_behavior = 0

   // The higher this is, the more likely the monster
   // will pick the highest level player in the room.
   piPlayer_factor = 10

   // Any monster flagged with this is much more likely to
   // engage in very deadly behavior.  Used to make orcs deadly.
   piHyperAggressive_factor = 60

   // The higher this is, the less likely monsters will 'swarm'.
   piChaser_factor = 100

   // The higher this number is, the more likely a wizard hater
   // will chase after wizards.
   piWizard_factor = 25

   // THe higher this number, the more likely a karma hater
   // will choose someone of the opposite karma.
   piKarma_factor = 25

   // These two variables control how much hatred is removed
   // by invisibility and shadow form.
   piInvis_reduction = 100
   piShadForm_reduction = 60

messages:

   MobConstructor(mob = $)
   {
      Send(mob,@EnterStateLimbo);

      return;
   }

   MobDelete(mob = $, state = $)
   {
      Send(mob,@EnterStateLimbo);

      return;
   }

   // 'What' is the new owner.  monster.poOwner is current owner.
   NewOwner(mob = $, state = 0, what = $)
   {
      local delay, oFormerRoom;

      if what = $
      {
         Debug("Unreachable. Null new owner.");
         Send(mob,@EnterStateLimbo);

         return;
      }

      if NOT state & STATE_LIMBO
      {
         Debug("Unreachable. Not in state LIMBO.");

         return;
      }

      oFormerRoom = Send(mob,@GetOwner);
      if oFormerRoom = $
      {
         delay = MOB_START_FREEZE;
      }
      else
      {
         delay = MOB_MOVE_TIMER_WAIT;
      }

      Send(mob,@EnterStateWait,#delay=delay);

      return;
   }

   FirstUserEntered(mob=$, state=$, what=$, new_row=$, new_col=$)
   {
      // Ensures we don't override a wait set by SomethingEntered
      if state & STATE_WAIT
      {
         return;
      }

      if state & STATE_LIMBO
      {
         Send(mob,@EnterStateMove,#actnow=False);

         return;
      }

      // Monsters should ONLY be waiting or moving at this point.
      if (state & STATE_CHASE) OR (state & STATE_ATTACK)
      {
         Debug("Unreachable. Illegal state",state," for ",mob," in room ",
               Send(Send(mob,@GetOwner),@GetName));
      }

      return;
   }

   LastUserLeft(mob=$, state=$)
   {
      if state & STATE_LIMBO
      {
         Debug("Unreachable. In state LIMBO.");
      }

      // Don't force a monster in limbo if it has persistent timers.
      if (NOT (Send(mob,@GetBehavior) & AI_MOVE_WHEN_ALONE))
      {
         Send(mob,@EnterStateLimbo);
      }

      return;
   }

   //// State changes are posted rather than sent because the player
   //// has not been moved to the new coordinates yet.  An alternative
   //// solution would be to have monster post this message to brain.
   //// In fact, this might be preferable, since then the special XLocation
   //// calls would not be needed, as the player *would* be in the new
   //// location when this message was processed. Duh.
   SomethingMoved(mob=$, state=$, what=$, new_row=$, new_col=$,
                  fine_row=$, fine_col=$, ignoreWait=FALSE)
   {
      local iVisionRange, oTarget;

      if what = mob
      {
         return;
      }

      if what <> $ AND mob <> $
      {
         if IsClass(mob, &Monster) AND IsClass(what, &Player) AND NOT Send(mob, @CanMinionAttackPlayer, #who=what)
         {
            return FALSE;
         }

         if IsClass(mob, &Monster) AND IsClass(what, &Monster) AND NOT Send(mob, @CanMinionAttackMonster, #what=what)
         {
            return FALSE;
         }
      }
      
      // If my master or master's master is what moved, let's not reset our behavior - let's keep on our existing.
      if what <> $ AND IsClass(what, &Player) AND IsClass(mob, &Monster) AND Send(mob, @IsMinionOf, #who=what)
      {
         //Debug(Send(mob,@GetName),"Something moved is relative master",Send(what,@GetName));
         return;
      }

      if what <> $ AND IsClass(what, &Player) AND NOT IsClass(mob, &Reflection) AND NOT IsClass(mob, &Servant) AND NOT Send(what,@HasAggro,#what=mob)
      {
         //Debug(Send(mob,@GetName),"detected",Send(what,@GetName));
         
         Send(what,@RecalibrateAggro);

         if Send(what,@GetAggroCount) >= MAX_AGGRO AND NOT Send(what,@HasAggro,#what=mob)
         {
            // Ignore the user, they are already being aggro'd by too many things.
            //Debug(Send(what,@GetName),"The player has too much aggro.",Send(mob,@GetName));
            return;
         }
      }

      if Send(mob, @GetTarget) = what
      {
         // Let's stay on our target.
         //Debug(Send(mob,@GetName),"is already targeting",Send(what,@GetName));
         return;
      }
      
      /// We now process all SomethingMoved calls for fluidity.
      //if (ignoreWait = FALSE)
      //   AND Random(1,5) <> 5
      //{
      //   return;
      //}

      if state & STATE_LIMBO
      {
         if Send(mob,@GetOwner) <> $
         {
            //Debug(Send(mob,@GetName),"is moving");
            Send(mob,@EnterStateMove,#actnow=FALSE);
         }

         return;
      }

      // Ignore moves if in WAIT
      if state & STATE_WAIT
         AND (ignoreWait = FALSE)
      {
         //Debug(Send(mob,@GetName),"is waiting",Send(what,@GetName));
         return;
      }

      // If they aren't our target already and aren't a valid target for
      // us in general, do nothing.
      if NOT Send(self,@AttemptTargetSwitch,#what=what,#event=TS_MOVE,#mob=mob)
      {
         //Debug(Send(mob,@GetName),"could not switch targets",Send(what,@GetName));
         return;
      }

      oTarget = Send(mob,@GetTarget);

      //Debug(Send(mob,@GetName),"targetted",Send(what,@GetName));

      // Some other monster may have already killed this target on the same
      // SomethingMoved.  If so, this don't fly no more, do it?
      if Send(mob,@GetOwner) <> Send(what,@GetOwner)
      {
        // Debug(Send(mob,@GetName),"rooms are different",Send(what,@GetName));
         return;
      }
	  
	  // Reflections should only attack if provoked
	//   if what <> $ AND mob <> $ AND IsClass(what, &Player) AND IsClass(mob, &Reflection) AND NOT Send(what, @HasAttacked, #victim=mob)
	//   {
	//      return;
	//   }
	  
	  if (state & STATE_MOVE) OR (state & STATE_WAIT)
     {
         if Send(mob,@CanReachLocation,#row=new_row,#col=new_col,
                  #fine_row=fine_row,#fine_col=fine_col)
         {
		      //Debug(Send(mob,@GetName),"is attacking (259)",Send(what,@GetName));
            Send(mob,@EnterStateAttack,#actnow=True);   // Used to be post.

            return;
         }

         Send(mob,@EnterStateChase,#target=what,#actnow=True); // Used to be post.

         return;
      }

      if state & STATE_CHASE
      {
         if Send(mob,@CanReachLocation,#row=new_row,#col=new_col,
                  #fine_row=fine_row,#fine_col=fine_col)
         {
		      //Debug(Send(mob,@GetName),"is attacking (275)",Send(what,@GetName));
            Send(mob,@EnterStateAttack,#target=what,#actnow=True); // Used to be post.
         }

         return;
      }

      if state & STATE_ATTACK
      {
         if NOT Send(mob,@CanReachLocation,#row=new_row,#col=new_col,
                     #fine_row=fine_row,#fine_col=fine_col)
         {
            //Debug(Send(mob,@GetName),"is chasing",Send(what,@GetName));
            Send(mob,@EnterStateChase,#target=what,#actnow=True); // Used to be post.

            return;
         }

         return;
      }

      return;
   }

   SomethingAttacked(mob=$, state=$, what=$, victim=$, use_weapon=$)
   {
      local oTarget, iType, bSwitch, oMaster;

      oTarget = Send(mob,@GetTarget);

      //// Sanity check
      if what = $
      {
         //Debug("BAD: null <what> in SomethingAttacked.");
      }

      if what = victim   // Earthquake style attack spell.
      {
         return;
      }
	  
      if mob <> $ AND what <> $ AND victim <> $
      {
         if IsClass(mob, &Monster) AND IsClass(victim, &Player) AND IsClass(mob, &Reflection) AND Send(mob, @CanMinionAttackPlayer, #who=victim) AND NOT Send(mob, @IsMinionOf, #who=victim)
         {
            //Debug("Minion attacked player, pvp gong", GetClass(mob), Send(mob,@GetName),"vs",Send(victim,@GetName));
            // Flag the victim for pvp
            Send(victim, @DoPvpNotify);

            // Flag the minion's master for pvp
            oMaster = Send(mob, @GetMaster);

            if oMaster <> $ AND IsClass(oMaster, &Servant)
            {
               oMaster = Send(oMaster, @GetMaster);
            }

            if oMaster <> $ AND IsClass(oMaster, &Player)
            {
               //Debug("Minion Master pvp gong", GetClass(oMaster),Send(oMaster,@GetName));
               Send(oMaster, @DoPvpNotify);
            }
         }

         if IsClass(mob, &Monster) AND IsClass(victim, &Player) AND NOT Send(mob, @CanMinionAttackPlayer, #who=victim)
         {
            return FALSE;
         }

         if IsClass(mob, &Monster) AND IsClass(victim, &Monster) AND NOT Send(mob, @CanMinionAttackMonster, #what=victim)
         {
            return FALSE;
         }
      }
            
      if what <> $ AND mob <> $ 
         AND (IsClass(what, &Reflection) AND IsClass(mob, &Reflection))
         OR IsClass(what, &Servant) AND IsClass(mob, &Servant)
      {
         // Prevent any kind of target switching if the enemies are servants or reflections.
         return;
      }

	  if IsClass(what, &Monster) AND IsClass(victim, &Player) AND NOT IsClass(what, &Servant) AND NOT IsClass(what, &Reflection)
	  {
	     // Monster attacked player
		 // Make sure the monster is known as an aggressor.
		 Send(victim,@AddAggro,#what=what);
	  }
	  
	  if what <> $ AND victim <> $ AND IsClass(what, &Player) AND IsClass(victim, &Monster) AND NOT IsClass(victim, &Servant) AND NOT IsClass(victim, &Reflection)
	  {
	    // Player attacked monster
		 // Make sure the monster is known as an aggressor.
       // Force it to be added to aggro!
		 Send(what,@AddAggro,#what=victim,#force=TRUE);
	  }

      //Debug(Send(what,@GetName),"attacked",Send(victim,@GetName));

      iType = TS_FIGHT;
      if victim = mob
      {
         iType = TS_FIGHT_ME_VICTIM;

         // Monster needs to record the time, used for when players flee it.
         Send(mob,@RecordTimeAttacked,#who=what);
      }
      else
      {
         // Always respond to your own attacks.
         // Respond to others half the time.
         if Random(1,10) < 6
            OR (Send(mob,@GetBehavior) & AI_FIGHT_MURDERERS)
         {
            return;
         }
      }

      if what = mob
      {
         iType = TS_FIGHT_ME_AGGRESSOR;
      }

      if (state & STATE_WAIT) OR (state & STATE_MOVE)
      {
         if Send(self,@AttemptTargetSwitch,#what=what,#event=iType,#mob=mob)
         {
            if Send(mob,@CanReach,#what=what)
            {
			   //Debug("AttackNow323",GetClass(mob),GetClass(what));
               Send(mob,@EnterStateAttack,#target=what,#actnow=True);

               return;
            }

            // If they hit us, we chase them whether we can see them or not.
            Send(mob,@EnterStateChase,#target=what,#actnow=True);

            return;
         }
      }

      if state & STATE_CHASE
      {
         bSwitch = FALSE;
         if iType <> TS_FIGHT_ME_VICTIM
            AND Send(mob,@CanReach,#what=victim)
         {
            bSwitch = Send(self,@AttemptTargetSwitch,#what=victim,
                           #event=iType,#mob=mob);
         }

         if iType <> TS_FIGHT_ME_AGGRESSOR
            AND Send(mob,@CanReach,#what=what)
         {
            bSwitch = Send(self,@AttemptTargetSwitch,#what=what,
                           #event=iType,#mob=mob);
         }

         if bSwitch
         {
            Send(mob,@EnterStateAttack,#actnow=True);

            return;
         }

         // If we can't reach them, we keep chasing our original target.
         // Note however that if the attacker is closer to us than our
         // target, XChaseTimer will switch us to chasing the attacker
         // (assuming he is a valid target for us at all times and not
         // only when he attacks)
         return;
      }

      if state & STATE_ATTACK
      {
         return;
      }

      return;
   }

   PlayerFirstMove(mob=$, state=$, what=$, where=$, new_row=0, new_col = 0)
   {
      Send(self,@SomethingMoved,#mob=mob,#state=state,#what=what,
            #where=where,#ignoreWait=TRUE,
            #new_row=new_row,#new_col=new_col);

      return;
   }

   SomethingEntered(mob=$, state=$, what=$, where=$)
   {
//      local iVision;

//      if IsClass(what,&Battler) AND NOT (what = mob)
//      {
//         if (state&STATE_ATTACK)=0
//         {
//            if Send(mob,@CanSee,#what=what) AND
//            Send(self,@AttemptTargetSwitch,#what=what,#event=TS_MOVE,#mob=mob)
//            {
//              // Changing non-combat state --- is this necessary?
//              // Should it be elsewhere?
//               Send(mob,@ClearBasicTimers);
//               Send(mob,@StartBasicTimers);
//               Send(mob,@EnterStateWait);
//            }
//         }
//      }

      return;
   }

   SomethingLeft(mob=$, state=$, what=$)
   {
      local oTarget;

      oTarget = Send(mob,@GetTarget);

      // This happens because LastUserLeft is sent before SomethingLeft.
      if state & STATE_LIMBO
      {
         return;
      }

      if what = oTarget
      {
         Send(mob,@EnterStateMove,#actnow=False);
      }

      return;
   }

   SomethingKilled(mob=$, state=$, what=$, victim=$, use_weapon=$)
   "Effects of killing are handled in response to SomethingLeft and"
   "LastUserLeft.  Special cases handled here: "
   "1. guards being activated by a murder of a white-name."
   "2. monster mourning the death of his master."
   {
      if IsClass(what,&User) AND IsClass(victim,&User)
         AND NOT Send(victim,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
         AND NOT Send(victim,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
         AND (Send(mob,@GetBehavior) & AI_FIGHT_MURDERERS)
      {
         if Send(self,@AttemptTargetSwitch,#what=what,#event=TS_MURDER,#mob=mob)
         {
            if Send(mob,@CanReach,#what=what)
            {
			   //Debug("AttackNow443",GetClass(mob),GetClass(what));
               Send(mob,@EnterStateAttack,#target=what,#actnow=True);

               return;
            }

            // If they hit us, we chase them whether we can see them or not.
            Send(mob,@EnterStateChase,#target=what,#actnow=True);

            return;
         }
      }

      if victim = Send(mob,@GetMaster)
      {
         if Send(self,@AttemptTargetSwitch,#what=what,#event=TS_MURDER,#mob=mob)
         {
            if Send(mob,@CanReach,#what=what)
            {
			   //Debug("AttackNow462",GetClass(mob),GetClass(what));
               Send(mob,@EnterStateAttack,#target=what,#actnow=True);
               return;
            }

           // If they hit us, we chase them whether we can see them or not.
		    //Debug("AttackNow468",GetClass(mob),GetClass(what));
            Send(mob,@EnterStateChase,#target=what,#actnow=True);

            return;
         }
      }

      return;
   }

   PostAttackTimer(mob = $, state = $)
   {
      local oTarget;

      if NOT (state & STATE_ATTACK)
      {
         return;
      }

      oTarget = Send(mob,@GetTarget);
	  //Debug("AttackNow488",GetClass(mob),GetClass(oTarget));
      Send(mob,@EnterStateAttack,#target=oTarget,#actnow=False);

      return;
   }

   // Returning True means state has changed
   PreChaseTimer(mob = $)
   {
      local oTarget;

      oTarget = Send(mob,@GetTarget);
      if Send(mob,@CanReach,#what=oTarget)
      {
         Send(mob,@EnterStateAttack,#target=oTarget,#actnow=True);

         return True;
      }

      return False;
   }

   PostChaseTimer(mob = $)
   {
      local oTarget,oNearest;

      oTarget = Send(mob,@GetTarget);
      if IsClass(oTarget,&User)
         AND Send(oTarget,@CheckPlayerFlag,#flag=PFLAG_INVISIBLE)
         AND NOT IsClass(mob,&Revenant)
      {
         if Random(1,50) = 1
         {
            Send(mob,@EnterStateMove,#actnow=True);
            return True;
         }
      }

      Send(mob,@EnterStateChase,#target=oTarget,#actnow=False);

      return;
   }

   PostMoveTimer(mob = $)
   {
      Send(mob,@EnterStateMove,#actnow=False);

      return;
   }

   PostWaitTimer(mob = $)
   {
      Send(mob,@EnterStateMove,#actnow=True);

      return;
   }

   Reset(mob = $)
   {
      Send(mob,@EnterStateMove,#actnow=True);

      return;
   }

   ////
   //// Utility Routines
   ////

   GetBrainNum()
   {
      return viBrain_num ;
   }

   GetMoveBehavior(behavior=0, mob = $)
   "This is called before the monster has a target.  Logic as follows: "
   "NOTE: With the new movement timer, this is called often enough to allow a"
   "      seamless move by calling MoveTowards/MoveToMaster in here."
   "      This means it runs more often and e.g. RandomWalks must be limited"
   "1. Do nothing if immobile."
   "2. If blind, wander aimlessly."
   "3. If frightened, run for your life."
   "4. If you have a master, go to his side."
   "5. Otherwise, wander aimlessly."
   {
      local oMaster, oOwner, oMasterRoom, oFrightener, bDoMoveMaster;

      if mob = $
      {
         Debug("Unreachable!  Called without argument!");

         return;
      }

      if (behavior & AI_NOMOVE)
      {
         // Monster is immobile.
         return;
      }

      // Example, monster is blind. Forced randomness has precedence
      // over everything else.
      if (behavior & AI_MOVE_RANDOM)
      {
         // skip 9/10 of the possible steps
         if Random(1,100) < 10
         {
           Send(mob,@DoRandomWalk);
         }

         return;
      }

      if (behavior & AI_HAS_TARGET_LOCATION)
      {
         Send(mob,@DoGoTowardsLocation);

         return;
      }

      if (behavior & AI_CAN_PATROL)
      {
         if (Random(1,10000) < Send(mob,@GetChanceToPatrol))
         {
            Send(mob,@StartNextPatrolLeg);
         }
      }

      if (behavior & AI_MOVE_FLEE_FRIGHTENERS)
      {
         oFrightener = Send(mob,@GetClosestFrightener);
         if oFrightener <> $
         {
            Send(mob,@MoveAway,#oTarget=oFrightener,#face_away=TRUE);

            return;
         }
      }

      if (behavior & AI_MOVE_FOLLOW_MASTER)
      {
         oMaster = Send(mob,@GetMaster);
         if oMaster <> $
         {
            oOwner = Send(mob,@GetOwner);
            oMasterRoom = Send(oMaster,@GetOwner);
            if oOwner = oMasterRoom
            {
			   bDoMoveMaster = FALSE;
			   
               /// Attempt to move towards master.
			   if IsClass(mob, &Servant)
			   {
			      // Only move to master if we're too far away.
				  if (Send(mob,@SquaredFineDistanceTo3D,#what=oMaster) / 100) > 100
				  {
				     bDoMoveMaster = TRUE;
				  }
			   }
			   
			   if bDoMoveMaster
			   {
                  Send(mob,@MoveToMaster);
			   }
            }
            else
            {
               // Master is not in room.  If master is a player,
               // teleport to player, to look like we're following
               // but only if we're a reflection or undead. Other cases
               // handled in monster.kod.
               if IsClass(mob,&Reflection) // Flecs follow.
                  OR Send(mob,@IsUndead) // So do undead (Animate).
               {
                  Post(mob,@GotoMaster);
               }
            }

            return;
         }
      }

      // If in doubt, wander aimlessly.
      // skip 9/10 of the possible steps
      if (Random(1,100) < 10 AND NOT (behavior & AI_CAN_PATROL))
      {
         Send(mob,@DoRandomWalk);
      }

      return;
   }

   GetAttackBehavior(behavior=0, mob = $, target = $)
   "This is called when monster has a target, but isn't close enough to fight."
   "1. Do nothing if immobile."
   "2. If blind, wander aimlessly."      
   "3. If we are terrified of what we are fighting, run away."
   "3. If wounded below viWimpy, either run away, or towards closest ally."
   "4. Try to move to optimal range, if possible."
   "5. Otherwise, move around target, but stay in range."
   {
      local oMaster, oOwner, oMasterRoom, oFrightener, oAlly, iRange, iOpt_range, oForm;

      if mob = $ OR target = $
      {
         Debug("Unreachable!  Called without argument!");

         return;
      }

      if (behavior & AI_NOMOVE)
      {
         // Monster is immobile.
         return;
      }

      // Example, monster is blind. Forced randomness has precedence
      // over everything else. Panic and move several times.
      if (behavior & AI_MOVE_RANDOM)
      {
         Send(mob,@DoRandomWalk);

         return;
      }

      if (behavior & AI_MOVE_FLEE_FRIGHTENERS)
      {
         // Okay, if the mob is FIGHTING someone he's frightened of,
         // try to get away.
         oForm = Send(target,@GetIllusionForm);
         if oForm = $
         {
            oForm = target;
         }
         if Send(mob,@IsFrightenedOf,#target=oForm)
         {
            Send(mob,@MoveAway,#oTarget=target,#face_away=TRUE);

            return;
         }
      }

      if Send(mob,@HealthBelowWimpy)
      {
         // If regroup flag set, monster will seek allies.
         if (behavior & AI_MOVE_REGROUP)
         {
            oAlly = Send(mob,@GetClosestAlly);
            if oAlly <> $
            {
               Send(mob,@MoveTowards,#oTarget=oAlly,#Face_target=TRUE);

               return;
            }
            else
            {
               // Mob has no allies available.  But he's not a wimp,
               // he'll stay and fight to the end.
               Send(mob,@MoveTowards,#oTarget=target,#face_target=TRUE);

               return;
            }
         }
         // Mob is a chicken, running from attacker.
         Send(mob,@MoveAway,#oTarget=target,#face_away = TRUE);

         return;
      }

      if (behavior & AI_MOVE_OPTIMAL_RANGE)
      {
         // Optimal range may not be fighting range.  Example,
         // shamans may want to be optimal spellcasting range away!
         iOpt_range = Send(mob,@GetOptimalRange);

         iRange = Send(mob,@SquaredFineDistanceTo3D,#what=target);
         if iRange < (iOpt_range * iOpt_range)
         {
            Send(mob,@MoveAway,#oTarget=target,#face_target=TRUE);

            return;
         }
      }

      // Okay, at this point, we want the monster to move,
      // but only cosmetically, and staying in range all the while.
      if (Send(mob,@GetOwner) = Send(target,@GetOwner))
         AND (Random(1,20) < Send(mob,@GetDifficulty))
      {
         Send(mob,@MoveTowards,#oTarget=target,#face_target=TRUE);
      }

      return;
   }

  GetChaseBehavior(behavior=0, mob = $, target = $)
   "This is called when monster has a target, but isn't close enough to fight."
   "1. Do nothing if immobile."
   "2. If blind, wander aimlessly."
   "3. If frightened, run for your life."
   "4. If you have a master who is closer than your target, go to his side."
   "5. If wounded below viWimpy, either run away, or towards closest ally."
   "6. Otherwise, move towards target."
   {
      local oMaster, oOwner, oMasterRoom, oFrightener,
            oAlly, iRange, iOpt_range, iSquaredVision, iSquaredDistance;

      if mob = $ OR target = $
      {
         Debug("Unreachable! GetChaseBehavior called without argument!");

         return;
      }

      if (behavior & AI_NOMOVE)
      {
         // Monster is immobile.
         return;
      }

      // Example, monster is blind. Forced randomness has precedence
      // over everything else. Panic and move several times.
      if (behavior & AI_MOVE_RANDOM)
      {
         // skip 9/10 of the possible steps
         if Random(1,100) < 10
         {
            Send(mob,@DoRandomWalk);
            return;
         }
      }

      if (behavior & AI_MOVE_FLEE_FRIGHTENERS)
      {
         oFrightener = Send(mob,@GetClosestFrightener);
         if oFrightener <> $
         {
            Send(mob,@MoveAway,#oTarget=oFrightener,#face_away=TRUE);

            return;
         }
      }

      if (behavior & AI_MOVE_FOLLOW_MASTER)
      {
         // No cross room travel. Target is assumed closer
         // if master is in another room.
         oMaster = Send(mob,@GetMaster);
         if oMaster <> $
         {
            // Only move towards master if he's closer, and
            // if he's in the same room.
            if Send(mob,@GetOwner) = Send(oMaster,@GetOwner)
               AND Send(mob,@SquaredDistanceTo,#what=oMaster)
                     < Send(mob,@SquaredDistanceTo,#what=target)
            {
               // Attempt to move towards master.
               Send(mob,@MoveToMaster);

               return;
            }
         }
      }

      if Send(mob,@HealthBelowWimpy)
      {
         // If regroup flag set, monster will seek allies.
         if (behavior & AI_MOVE_REGROUP)
         {
            oAlly = Send(mob,@GetClosestAlly);
            if oAlly <> $
            {
               Send(mob,@MoveTowards,#oTarget=oAlly,#face_target=TRUE);

               return;
            }
            else
            {
               // Mob has no allies available.  But he's not a wimp,
               // he'll stay and fight to the end.
               Send(mob,@MoveTowards,#oTarget=target,#face_target=TRUE);

               return;
            }
         }
         // Mob is a chicken, running like hell from attacker.
         Send(mob,@MoveAway,#oTarget=target,#face_away=TRUE);

         return;
      }

      if (behavior & AI_MOVE_OPTIMAL_RANGE)
      {
         // Optimal range may not be best fighting range.
         // shamans want to get far away and cast spells!
         iOpt_range = Send(mob,@GetOptimalRange);

         iRange = Send(mob,@SquaredFineDistanceTo3D,#what=target);
         if iRange < (iOpt_range * iOpt_range)
         {
            Send(mob,@MoveAway,#oTarget=target,#face_away=TRUE);

            return;
         }
      }

      // Check here if we need to break target, perhaps they are now
      // too far away and have escaped us. This is the same vision check
      // as used in GetHatred, with 5 added to the monster vision.
      if NOT (behavior & AI_FIGHT_HYPERAGGRESSIVE)
         AND NOT (behavior & AI_FIGHT_SINGLEMINDED)
         AND (Send(mob,@GetTimeAttacked) + 10) < GetTime()
      {
         iSquaredVision = Send(mob,@GetVisionDistance) + 5;
         iSquaredVision = (iSquaredVision * iSquaredVision);
         iSquaredDistance = (Send(mob,@SqFineDistToVision,#what=target)
                                 / FINENESS_SQUARED);
         if iSquaredVision < iSquaredDistance
         {
            Send(mob,@EnterStateWait,#actnow=TRUE);

            return;
         }
      }

      if Send(mob,@GetOwner) = Send(target,@GetOwner)
      {
         Send(mob,@MoveTowards,#oTarget=target,#face_target=TRUE);
      }
      else
      {
         Send(mob,@EnterStateLimbo);
      }

      return;
   }

   GetDefaultBehavior()
   {
      return piDefault_behavior;
   }

   GetHatred(mob=$,what=$,behavior=0,event=0)
   "Returns a value.  If this value is greater than the existing hatred, "
   "we attack. If iBalance ends up greater than 0, return TRUE."
   {
      local iBalance, oTarget, iHatred, iKarma1, iKarma2, oMaster,
            oApparentForm, oApparentTarget, bKarmaHater, iAdd, oMobTarget,
            iSquaredVision, iSquaredDistance;

      if mob = $ OR what = $
      {
         Debug("BadInfo passed to GetHatred!");

         return FALSE;
      }

      // Never switch to target yourself.
      if what = mob
      {
         return 0;
      }

      // These guys never target anyone at all.
      if (behavior & AI_NOFIGHT) OR (behavior & AI_NPC)
      {
         return 0;
      }

      iHatred = 0;

      oMobTarget = Send(mob,@GetTarget);
      if oMobTarget <> $
      {
         // Singleminded monsters (i.e. revenant) never switch targets.
         if (behavior & AI_FIGHT_SINGLEMINDED)
         {
            return 0;
         }

         // If I'm already fighting someone, I probably won't
         // switch a lot.  I'm less likely to switch if he's
         // attacking me, too.
         iHatred = iHatred - 10;
         if Send(oMobTarget,@GetTarget) = mob
         {
            iHatred = iHatred - 15;
         }
      }

      oMaster = Send(mob,@GetMaster);

      // Minions do not attack players unless provoked.
      if oMaster <> $
      {
         if IsClass(oMaster,&User)
            AND IsClass(what,&User)
            AND event <> TS_FIGHT_ME_VICTIM
         {
            if NOT Send(SYS,@IsPKAllowed)
            {
               return 0;
            }
         }

         // Never switch to target your master
         // UNLESS your master is attacking you.
         if oMaster = what
         {
            if event <> TS_FIGHT_ME_VICTIM
            {
               return 0;
            }
            else
            {
               Send(mob,@SetBehaviorFlag,#flag=AI_FIGHT_SINGLEMINDED);

               return 1000;
            }
         }

         // Don't attack faction soldier NPCs unless
         // they are attacking the master.
         if IsClass(what,&FactionTroop)
            AND oMaster <> Send(what,@GetTarget)
         {
            return 0;
         }
      }

      // Tend to not favor invis or shadowform.
      if event <> TS_FIGHT_ME_VICTIM
      {
         if Send(what,@IsInvisible)
         {
            iHatred = (iHatred - piInvis_reduction);
         }
         else
         {
            if Send(what,@IsShadowForm)
            {
               iHatred = (iHatred - piShadForm_reduction);
            }
         }
      }

      /// If they aren't in the same room, don't switch to them.
      if Send(mob,@GetOwner) <> Send(what,@GetOwner)
      {
         return 0;
      }

      // if it was a murder of a whitename and I'm a guard, or it was the
      // death of my master, then go hard after the killer.  Note, we've
      // already checked to see if it's a murder in SomethingKilled().
      if event = TS_MURDER
      {
         iHatred = (iHatred + 75);
      }

      oTarget = Send(what,@GetTarget);

      if (oTarget <> $) AND (NOT IsObject(oTarget))
      {
         // Monster makes no decision if the target is invalid.
         Debug("Got",oTarget,"for target of what",GetClass(what),
               "and mob",GetClass(mob));
         oTarget = $;

         return 0;
      }

      oApparentTarget = $;
      if (oTarget <> $) AND IsClass(oTarget,&Battler)
      {
         oApparentTarget = Send(oTarget,@GetIllusionForm);
      }
      if oApparentTarget = $
      {
         oApparentTarget = oTarget;
      }

      // Might think a player feigning death is actually dead.
      if (oApparentTarget <> $)
         AND IsClass(oApparentTarget,&DeadBody)
      {
         if (Random(0,150) > Send(mob,@GetMaxHitPoints))
         {
            return 0;
         }
         else
         {
            oApparentTarget = oTarget;
         }
      }

      if (oApparentTarget <> $) AND (NOT IsObject(oApparentTarget))
      {
         // Monster makes no decision if the apparent target is invalid.
         Debug("Got",oApparentTarget,"for apparent target of what",
               GetClass(what),"and mob",GetClass(mob));
         oApparentTarget = $;

         return 0;
      }

      if oTarget <> $
      {
         // If either of them is targetting our master, that goes against them.
         if oTarget = oMaster
         {
            iHatred = (iHatred + 60);
         }

         // If they are fighting us, that's a little less important.
         if oTarget = mob
         {
            iHatred = (iHatred + 40);
         }

         // If they are fighting an ally, that's not as important.
         // Still, help out your buds, even if they are morphed to fool you.
         if Send(mob,@IsAlly,#target=oApparentTarget)
         {
            iHatred = (iHatred + 20);
         }
      }

      // Never switch to target an ally, UNLESS he is not of his own free will.
      if Send(mob,@IsAlly,#target=what) AND oTarget <> mob
         AND oMaster = $ AND Send(what,@GetMaster) = $
      {
         return 0;
      }

      // If my master wants to kill this guy, so do i.
      if oMaster <> $ AND Send(oMaster,@GetTarget) = what
      {
         iHatred = (iHatred + 50);
      }

      /// Reflection spells can fool monsters.
      oApparentForm = Send(what,@GetIllusionForm);
      if oApparentForm = $
      {
         oApparentForm = what;
      }
      else
      {
         if oApparentForm = oMobTarget
         {
            iHatred = (iHatred  + 20);
         }
      }

      if IsClass(what,&Monster)
      {
         if NOT (behavior & AI_FIGHT_MONSTERS)
         {
            if event <> TS_FIGHT_ME_VICTIM
            {
               return 0;
            }
            else
            {
               iHatred = (iHatred -20);
            }
         }
      }
      else
      {
         if (behavior & AI_FIGHT_NEWBIESAFE)
            AND NOT Send(what,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
         {
            if event = TS_MOVE
            {
               return 0;
            }
            else
            {
               iHatred = (iHatred - 20);
            }
         }
      }

      bKarmaHater = FALSE;
      if (behavior & AI_FIGHT_KARMA_AGGRESSIVE)
      {
         if IsClass(what,&player) OR (behavior & AI_FIGHT_MONSTERS)
         {
            iKarma1 = Send(mob,@GetKarma);
            iKarma2 = 0;

            // What does mob THINK his target's karma is.
            if IsClass(oApparentForm,&Battler)
            {
               iKarma2= Send(oApparentForm,@GetKarma);
            }
            if iKarma1 <> 0 AND iKarma2 <> 0
            {
               if (abs(iKarma1)/iKarma1) <> (Abs(iKarma2)/iKarma2)
               {
                  iHatred = (iHatred + ((Abs(iKarma2)* piKarma_factor)/100));
                  bKarmaHater = TRUE;
               }
            }
         }
      }

      if (behavior & AI_FIGHT_MURDERERS)
      {
         if IsClass(oApparentForm,&User)
            AND Send(oApparentForm,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
         {
            iHatred = (iHatred + 50 + Send(what,@GetUnjustifiedKills)*4);
         }
      }

      if (behavior & AI_FIGHT_WIZARD_KILLER)
      {
         if IsClass(oApparentForm,&Battler)
            AND Send(oApparentForm,@IsWizard)
         {
            iHatred = (iHatred + piWizard_factor);
         }
      }

      iHatred = iHatred + Send(mob,@SpecialHatredBehavior,#what=what);

      // Basic sense of self-preservation:
      iAdd = 0;
      if (event = TS_FIGHT_ME_VICTIM)
      {
         if oTarget = $
         {
            iAdd = 100;
         }
         else
         {
            iAdd = Send(mob,@GetLevel)/2;
         }
      }

      iHatred = iHatred + iAdd;
      iAdd = 0;

      if iHatred <= 0 AND NOT (behavior & AI_FIGHT_AGGRESSIVE)
      {
	     return 0;
      }

      // This is the monster vision check. If 'what' attacked us, skip
      // this since we will attack regardless of where 'what' is (even
      // if we can't see them, we can see where the attack comes from).
      if NOT (event = TS_FIGHT_ME_VICTIM)
      {
         // This is the actual range past which a monster can't 'see'. We
         // use this as our base for this calculation.
         iSquaredVision = Send(mob,@GetVisionDistance);
         iSquaredVision = (iSquaredVision * iSquaredVision);

         // Use the new SqFineDistToVision here for better resolution.
         // Based on SquaredFineDistanceTo, but without $ checks for speed.
         iSquaredDistance = (Send(mob,@SqFineDistToVision,#what=what)
                                    / FINENESS_SQUARED);

         // If outside our vision range, return 0 and don't switch.
         if iSquaredVision < iSquaredDistance
         {
            return 0;
         }
         
         iAdd = Sqrt(iSquaredVision - iSquaredDistance) * 2;
      }
      iHatred = iHatred + iAdd;

      if (event = TS_ENTRY) and oMobTarget = $
      {
         iHatred = iHatred + 5;
      }
      if (event = TS_ENTRY_APPROVED)
      {
         iHatred = iHatred + 20;
      }

      // Up to 20 points for level.
      if IsClass(what,&Monster)
      {
         iHatred = (iHatred + Bound(Send(what,@GetLevel),1,100)/5);
      }
      else
      {
         // Give newbies a break.
         iHatred = (iHatred + (Bound(Send(what,@GetMaxHealth)-20,0,150)
                                    * piPlayer_factor)/100);

         // If they already have a lot of people chasing them, run away.
         if NOT (event = TS_FIGHT_ME_VICTIM)
         {
            iHatred = iHatred - (Send(what,@GetMonsterChasers)
                                       * piChaser_factor)/100;
         }
      }

      if (behavior & AI_FIGHT_HYPERAGGRESSIVE)
      {
         iHatred = iHatred + piHyperAggressive_factor;
      }

      return iHatred;
   }

   AttemptTargetSwitch(mob = $, what=$, event = 1)
   "Target Switching Behavior."
   {
      local iHatred, oldHatred, behavior, oTarget;

      if what = $
      {
         //Debug("Invalid info passed to AttemptTargetSwitch");

         return FALSE;
      }

      if mob <> $ ANd what <> $
      {
         if IsClass(mob, &Monster) AND IsClass(what, &Player) AND NOT Send(mob, @CanMinionAttackPlayer, #who=what)
         {
            return FALSE;
         }

         if IsClass(mob, &Monster) AND IsClass(what, &Monster) AND NOT Send(mob, @CanMinionAttackMonster, #what=what)
         {
            return FALSE;
         }
      }

      oTarget = Send(mob, @GetTarget);

      if NOT IsClass(what,&Battler) OR IsClass(what, &Servant)
      {
         //Debug("AttemptTargetSwitch Not Battler");
         return FALSE;
      }

      if NOT Send(what,@IsLikelyVictim)
      {
         //Debug("AttemptTargetSwitch Not Likely Victim");
         return FALSE;
      }

      if IsClass(mob,&Revenant) AND NOT IsClass(what,&Servant) AND NOT IsClass(what,&Reflection)
      {
         //Debug("AttemptTargetSwitch Is Revenant");
         return FALSE;
      }

	  if what <> $ AND IsClass(what, &Player) AND NOT Send(what,@HasAggro,#what=mob)
	  {
      //Debug("AttemptTargetSwitch Target Has Room for Aggro");
	     if Send(what,@GetAggroCount) >= MAX_AGGRO 
            AND NOT IsClass(mob, &Reflection) 
            AND NOT IsClass(mob, &EvilTwin) 
            AND NOT IsClass(mob, &Player)
		 {
		    // Ignore the user, they are already being aggro'd by too many things.
          // But allow reflections and evil twin to bypass aggro limits.
          //Debug("AttemptTargetSwitch", Send(mob, @GetName),"max aggro",Send(what,@GetName));
			return FALSE;
		 }
	  }
	  
      behavior = Send(mob,@GetBehavior);
      iHatred = Send(self,@GetHatred,#what=what,#mob=mob,
                     #event=event,#behavior=behavior);

      if behavior & AI_FIGHT_SWITCHALOT
         AND (oTarget <> $ AND NOT Send(mob,@CanReach,#what=oTarget))
         AND Send(mob,@CanReach,#what=what)
      {
         oldHatred = 0;
      }
      else
      {
         oldHatred = Send(mob,@GetHatred,#what=what);
      }

      if iHatred > oldHatred
      {
         //Debug("TargetSwitch Called", GetClass(mob), Send(mob, @GetName),"HATRED",GetClass(what),Send(what,@GetName));
         return Send(mob,@TargetSwitch,#what=what,#iHatred=iHatred);
      }

      //Debug("AttemptTargetSwitch FAILED", Send(mob, @GetName),"returned false",Send(what,@GetName), oldHatred,iHatred);

      if oTarget = what
      {
         // Stay on the same target.
         return TRUE;
      }

      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
